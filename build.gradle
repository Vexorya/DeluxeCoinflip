plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
}

group = 'net.zithium'
version = '2.7.13-VEX'
description = 'DeluxeCoinflip'

repositories {
    mavenCentral()
    maven { url = uri('https://repo.papermc.io/repository/maven-public/') }
    maven { url = uri('https://ci.ender.zone/plugin/repository/everything/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public/') }
    maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
    maven { url = uri('https://libraries.minecraft.net/') }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.nightexpressdev.com/releases' }
}

dependencies {
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'dev.triumphteam:triumph-gui:3.1.11'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly('com.github.Realizedd:TokenManager:3.2.4') { transitive = false }
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    compileOnly 'su.nightexpress.coinsengine:CoinsEngine:2.4.1'
    compileOnly fileTree(dir: 'libs', include: '*.jar')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

compileJava {
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    minimize()
    archiveFileName = "DeluxeCoinflip-${project.version}.jar"
    relocate 'dev.triumphteam.gui', 'net.zithium.deluxecoinflip.libs.gui'
    relocate 'me.mattstudios.mf', 'net.zithium.deluxecoinflip.libs.command'
    relocate 'org.bstats', 'net.zithium.deluxecoinflip.libs.metrics' // bStats
}

build {
    dependsOn shadowJar
}